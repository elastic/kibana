buildscript {
  repositories {
    jcenter()
  }
}

plugins {
  id "com.moowork.node" version "1.2.0"
}

node {
  version = rootProject.nodeVersion
  yarnVersion = rootProject.yarnVersion
  download = true
}

task bootstrap(type: YarnTask, dependsOn: [yarnSetup, rootProject.tasks.checkoutSubmodule])  {
  inputs.property("kibanaVersion", kibanaVersion)
  inputs.files(
    file("$rootDir/kibana-extra/codesearch/package.json"),
    file("$rootDir/kibana-extra/codesearch/yarn.lock"),
    file("$rootDir/kibana/yarn.lock"),
    file("$rootDir/kibana/x-pack/yarn.lock"),
    fileTree("$rootDir/kibana/packages") {
      include "*/yarn.lock"
    }
  )

  outputs.files(
    file("$rootDir/kibana-extra/codesearch/node_modules/.yarn-integrity"),
    file("$rootDir/kibana/node_modules/.yarn-integrity"),
    file("$rootDir/kibana/x-pack/node_modules/.yarn-integrity"),
    fileTree("$rootDir/kibana/packages") {
      include "*/node_modules/.yarn-integrity"
    }
  )

  args = ['kbn', 'bootstrap']
}

task startKibana(type: YarnTask, dependsOn: [bootstrap]) {
  args = ['start', '--logging.json=false']
}

if (!Boolean.valueOf(System.getenv('LSP_DETACH'))) {
  tasks.startKibana.dependsOn(":lsp:javascript-typescript-langserver:build")
}

task startFullKibana(type: YarnTask, dependsOn: [bootstrap, ":lsp:javascript-typescript-langserver:build", ":lsp:eclipse.jdt.ls:build"]) {
  args = ['start', '--logging.json=false']
}

task debugKibana(type: YarnTask, dependsOn: [bootstrap, ":lsp:javascript-typescript-langserver:build"]) {
  args = ['debug', '--logging.json=false']
}

task test(type: YarnTask) {
  args = ['test']
}

task startDeps(type: YarnTask, dependsOn: [bootstrap]) {
  args = ['start-deps']
}

task startCacheDeps(type: YarnTask, dependsOn: [bootstrap]) {
  args = ['start-cache-deps']
}

task checkAllFilenames(type: YarnTask, dependsOn: [bootstrap]) {
  args = ['check_all_filenames']
}

task lint(type: YarnTask, dependsOn: [bootstrap]) {
  args = ['tslint']
}

task lintFix(type: YarnTask, dependsOn: [bootstrap]) {
  args = ['tslint', '--fix']
}

task typeCheck(type: YarnTask, dependsOn: [bootstrap]) {
  args = ['type-check']
}

task typeCheckWatch(type: YarnTask, dependsOn: [bootstrap]) {
  args = ['type-check', '--watch']
}

tasks.withType(YarnTask) { task ->
  task.environment = ['FORCE_COLOR': 'true']
}
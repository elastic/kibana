// Initialization block
{
  const { nodeTypes } = options.helpers;
  const buildFunctionNode = nodeTypes.function.buildNodeWithArgumentNodes;
  const buildLiteralNode = nodeTypes.literal.buildNode;
  const buildWildcardNode = nodeTypes.wildcard.buildNode;
  const buildNamedArgNode = nodeTypes.namedArg.buildNode;

  function trimLeft(string) {
    return string.replace(/^[\s\uFEFF\xA0]+/g, '');
  }

  function trimRight(string) {
    return string.replace(/[\s\uFEFF\xA0]+$/g, '');
  }
}

start
  = Space* query:OrQuery? Space* {
    if (query !== null) return query;
    return nodeTypes.function.buildNode('is', '*', '*');
  }

OrQuery
  = left:AndQuery Or right:OrQuery {
    return buildFunctionNode('or', [left, right]);
  }
  / AndQuery

AndQuery
  = left:NotQuery And right:AndQuery{
    return buildFunctionNode('and', [left, right]);
  }
  / NotQuery

NotQuery
  = Not query:SubQuery {
    return buildFunctionNode('not', [query]);
  }
  / SubQuery

SubQuery
  = '(' Space* query:OrQuery Space* ')' { return query; }
  / Expression

Expression
  = FieldRangeExpression
  / FieldValueExpression
  / ValueExpression

FieldRangeExpression
  = field:Literal Space* operator:RangeOperator Space* value:(QuotedString / UnquotedLiteral) {
    const range = buildNamedArgNode(operator, value);
    return buildFunctionNode('range', [field, range]);
  }

FieldValueExpression
  = field:Literal Space* ':' Space* partial:ListOfValues {
    return partial(field);
  }

ValueExpression
  = partial:Value {
    const field = buildLiteralNode(null);
    return partial(field);
  }

ListOfValues
  = '(' Space* partial:OrListOfValues Space* ')' { return partial; }
  / Value

OrListOfValues
  = partialLeft:AndListOfValues Or partialRight:OrListOfValues {
    return (field) => buildFunctionNode('or', [partialLeft(field), partialRight(field)]);
  }
  / AndListOfValues

AndListOfValues
  = partialLeft:NotListOfValues And partialRight:AndListOfValues {
    return (field) => buildFunctionNode('and', [partialLeft(field), partialRight(field)]);
  }
  / NotListOfValues

NotListOfValues
  = Not partial:ListOfValues {
    return (field) => buildFunctionNode('not', [partial(field)]);
  }
  / ListOfValues

Value
  = value:QuotedString {
    const isPhrase = buildLiteralNode(true);
    return (field) => buildFunctionNode('is', [field, value, isPhrase]);
  }
  / value:WildcardString {
    const isPhrase = buildLiteralNode(false);
    return (field) => buildFunctionNode('is', [field, value, isPhrase]);
  }
  / value:UnquotedLiteral {
    const isPhrase = buildLiteralNode(false);
    return (field) => buildFunctionNode('is', [field, value, isPhrase]);
  }

Or
  = Space+ 'or'i Space+

And
  = Space+ 'and'i Space+

Not
  = 'not'i Space+

Literal
  = QuotedString / WildcardString / UnquotedLiteral

QuotedString
  = '"' chars:(EscapedDoubleQuote / [^"])* '"' {
    return buildLiteralNode(chars.join(''));
  }

WildcardString
  = sequences:WildcardSequence+ {
    const compactedSequences = sequences.reduce((acc, arr, i) => {
      const compacted = arr.filter(value => value !== '');
      return [...acc, ...compacted];
    }, []);
    if (typeof compactedSequences[0] === 'string') {
      compactedSequences[0] = trimLeft(compactedSequences[0]);
    }
    const lastIndex = compactedSequences.length - 1;
    if (typeof compactedSequences[lastIndex] === 'string') {
      compactedSequences[lastIndex] = trimRight(compactedSequences[lastIndex]);
    }
    return buildWildcardNode(compactedSequences);
  }

WildcardSequence
  = left:UnquotedCharacter* '*' right:UnquotedCharacter* {
    return [left.join(''), nodeTypes.wildcard.wildcardSymbol, right.join('')];
  }

UnquotedLiteral
  = chars:UnquotedCharacter+ {
    const sequence = chars.join('').trim();
    if (sequence === 'null') return buildLiteralNode(null);
    if (sequence === 'true') return buildLiteralNode(true);
    if (sequence === 'false') return buildLiteralNode(false);
    const number = Number(sequence);
    const value = isNaN(number) ? sequence : number;
    return buildLiteralNode(value);
  }

UnquotedCharacter
  = EscapedSpecialCharacter
  / !Separator char:. { return char; }

EscapedSpecialCharacter
  = '\\' char:SpecialCharacter { return char; }

EscapedDoubleQuote
  = '\\' char:'"' { return char; }

Separator
  = Keyword / SpecialCharacter

Keyword
  = Or / And / Not

SpecialCharacter
  = [\\():<>"*]

RangeOperator
  = '<=' { return 'lte'; }
  / '>=' { return 'gte'; }
  / '<' { return 'lt'; }
  / '>' { return 'gt'; }

Space
  = [\ \t\r\n]

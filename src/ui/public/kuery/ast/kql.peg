// Initialization block
{
  const { helpers, parseCursor } = options;
  const { nodeTypes: { function: { buildNode }}} = helpers;

  function getCursor(chars, location) {
    const i = chars.findIndex(char => char.type === 'cursor');
    return {
      type: 'cursor',
      prefix: chars.slice(0, i).join(''),
      suffix: chars.slice(i + 1).join(''),
      start: location.start.offset,
      end: location.end.offset - '@kql-cursor@'.length
    };
  }
}

start
  = OrQuery
  / EmptyQuery

EmptyQuery
  = Space* {
    return buildNode('and', []);
  }

OrQuery
  = left:AndQuery Or right:OrQuery {
    const cursor = [left, right].find(node => node.type === 'cursor');
    if (cursor) return cursor;
    return buildNode('or', [left, right]);
  }
  / AndQuery

AndQuery
  = left:NotQuery And right:AndQuery {
    const cursor = [left, right].find(node => node.type === 'cursor');
    if (cursor) return cursor;
    return buildNode('and', [left, right]);
  }
  / NotQuery

NotQuery
  = Not query:SubQuery {
    if (query.type === 'cursor') return query;
    return buildNode('not', query);
  }
  / SubQuery

SubQuery
  = '(' Space* query:OrQuery Space* ')' { return query; }
  / Expression

Expression
  = FieldListExpression
  / FieldRangeExpression
  / ValueExpression

FieldListExpression
  = field:Literal Space* ':' Space* list:SubList {
    function buildNodeWithField(node) {
      if (node.type === 'cursor') {
        return {
          ...list,
          fieldName: field,
          suggestionTypes: ['value', 'conjunction']
        };
      } else if (node.hasOwnProperty('arguments')) {
        const args = node.arguments.map(buildNodeWithField);
        return { ...list, arguments: args };
      } else {
        return buildNode('is', field, node);
      }
    }
    return buildNodeWithField(list);
  }

FieldRangeExpression
  = field:Literal Space* operator:RangeOperator Space* value:Literal {
    return buildNode('range', field, { [operator]: value });
  }

ValueExpression
  = value:Literal {
    if (value.type === 'cursor') {
      const fieldName = (value.prefix + value.suffix).trim();
      return {
        ...value,
        fieldName,
        suggestionTypes: ['field', 'operator']
      };
    }
    return buildNode('is', '*', value);
  }

OrList
  = left:AndList Or right:OrList {
    const cursor = [left, right].find(node => node.type === 'cursor');
    if (cursor) {
      return {
        ...cursor,
        suggestionTypes: ['value']
      };
    }
    return buildNode('or', [left, right]);
  }
  / AndList

AndList
  = left:NotList And right:AndList{
    const cursor = [left, right].find(node => node.type === 'cursor');
    if (cursor) {
      return {
        ...cursor,
        suggestionTypes: ['value']
      };
    }
    return buildNode('and', [left, right]);
  }
  / NotList

NotList
  = Not list:SubList {
    if (list.type === 'cursor') {
      return {
        ...list,
        suggestionTypes: ['value']
      };
    }
    return buildNode('not', query);;
  }
  / SubList

SubList
  = '(' Space* list:OrList Space* ')' {
    if (list.type === 'cursor') {
      return {
        ...list,
        suggestionTypes: ['value']
      };
    }
    return list;
  }
  / Literal

Or
  = Space+ 'or'i Space+

And
  = Space+ 'and'i Space+

Not
  = 'not'i Space+

Literal
  = SingleQuotedString
  / DoubleQuotedString
  / UnquotedLiteral

SingleQuotedString
  = "'" chars:(Cursor / EscapedQuote / [^'])* "'" {
    if (chars.find(char => char.type === 'cursor')) {
      return getCursor(chars, location());
    }
    return chars.join('');
  }

DoubleQuotedString
  = '"' chars:(Cursor / EscapedQuote / [^"])* '"' {
    if (chars.find(char => char.type === 'cursor')) {
      return getCursor(chars, location());
    }
    return chars.join('');
  }

UnquotedLiteral
  = chars:(UnquotedCharacter)+ {
    if (chars.find(char => char.type === 'cursor')) {
      return getCursor(chars, location());
    }
    const sequence = chars.join('').trim();
    if (sequence === 'null') return null;
    if (sequence === 'true') return true;
    if (sequence === 'false') return false;
    const number = Number(sequence);
    return isNaN(number) ? sequence : number;
  }

EscapedQuote
  = '\\' quote:('"' / "'") { return quote; }

UnquotedCharacter
  = Cursor
  / EscapedSpecialCharacter
  / !Separator char:. { return char; }

EscapedSpecialCharacter
  = '\\' char:SpecialCharacter { return char; }

Separator
  = Keyword / SpecialCharacter

Keyword
  = Or / And / Not

SpecialCharacter
  = [():<>]

RangeOperator
  = '<=' { return 'lte'; }
  / '>=' { return 'gte'; }
  / '<' { return 'lt'; }
  / '>' { return 'gt'; }

Space
  = [\ \t\r\n]

Cursor
  = &{ return parseCursor; } '@kql-cursor@' {
    return { type: 'cursor' };
  }

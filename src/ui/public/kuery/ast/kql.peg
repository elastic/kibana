// Initialization block
{
  var { nodeTypes: { function: { buildNode }}} = options.helpers;
}

start
  = OrQuery
  / EmptyQuery

EmptyQuery
  = Space* {
    return buildNode('and', []);
  }

OrQuery
  = left:AndQuery Or right:OrQuery {
    return buildNode('or', [left, right]);
  }
  / AndQuery

AndQuery
  = left:NotQuery And right:AndQuery{
    return buildNode('and', [left, right]);
  }
  / NotQuery

NotQuery
  = Not query:SubQuery {
    return buildNode('not', query);
  }
  / SubQuery

SubQuery
  = '(' Space* query:OrQuery Space* ')' { return query; }
  / Expression

Expression
  = FieldListExpression
  / FieldRangeExpression
  / ValueExpression

FieldListExpression
  = field:Literal Space* ':' Space* list:SubList {
    function buildNodeWithField(field, list) {
      if (list.hasOwnProperty('and')) {
        return buildNode('and', list.and.map(node => buildNodeWithField(field, node)));
      } else if (list.hasOwnProperty('or')) {
        return buildNode('or', list.or.map(node => buildNodeWithField(field, node)));
      } else if (list.hasOwnProperty('not')) {
        return buildNode('not', buildNodeWithField(field, list.not));
      } else {
        return buildNode('is', field, list);
      }
    }
    return buildNodeWithField(field, list);
  }

FieldRangeExpression
  = field:Literal Space* operator:RangeOperator Space* value:Literal {
    return buildNode('range', field, { [operator]: value });
  }

ValueExpression
  = value:Literal {
    return buildNode('is', '*', value);
  }

OrList
  = left:AndList Or right:OrList {
    return { or: [left, right] };
  }
  / AndList

AndList
  = left:NotList And right:AndList{
    return { and: [left, right] };
  }
  / NotList

NotList
  = Not list:SubList {
    return { not: list };
  }
  / SubList

SubList
  = '(' Space* list:OrList Space* ')' { return list; }
  / Literal

Or
  = Space+ 'or'i Space+

And
  = Space+ 'and'i Space+

Not
  = 'not'i Space+

Literal
  = SingleQuotedString
  / DoubleQuotedString
  / UnquotedLiteral

SingleQuotedString
  = "'" chars:(EscapedQuote / [^'])* "'" {
    return chars.join('');
  }

DoubleQuotedString
  = '"' chars:(EscapedQuote / [^"])* '"' {
    return chars.join('');
  }

UnquotedLiteral
  = chars:UnquotedCharacter+ {
    const sequence = chars.join('').trim();
    if (sequence === 'null') return null;
    if (sequence === 'true') return true;
    if (sequence === 'false') return false;
    const number = Number(sequence);
    return isNaN(number) ? sequence : number;
  }

EscapedQuote
  = '\\' quote:('"' / "'") { return quote; }

UnquotedCharacter
  = EscapedSpecialCharacter
  / !Separator char:. { return char; }

EscapedSpecialCharacter
  = '\\' char:SpecialCharacter { return char; }

Separator
  = Keyword / SpecialCharacter

Keyword
  = Or / And / Not

SpecialCharacter
  = [():<>]

RangeOperator
  = '<=' { return 'lte'; }
  / '>=' { return 'gte'; }
  / '<' { return 'lt'; }
  / '>' { return 'gt'; }

Space
  = [\ \t\r\n]

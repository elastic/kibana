/*
 * Kuery parser
 *
 * To generate the parsing module (legacy_kuery.js), run `grunt peg`
 * To watch changes and generate on file change, run `grunt watch:peg`
 */

/*
 * Initialization block
 */
{
  var nodeTypes = options.helpers.nodeTypes;

  if (options.includeMetadata === undefined) {
    options.includeMetadata = true;
  }

  function addMeta(source, text, location) {
    if (options.includeMetadata) {
      return Object.assign(
        {},
        source,
        {
          text: text,
          location: simpleLocation(location),
        }
      );
    }

    return source;
  }

  function simpleLocation(location) {
    // Returns an object representing the position of the function within the expression,
    // demarcated by the position of its first character and last character. We calculate these values
    // using the offset because the expression could span multiple lines, and we don't want to deal
    // with column and line values.
    return {
      min: location.start.offset,
      max: location.end.offset
    }
  }
}

start
  = space? query:OrQuery space? {
    if (query.type === 'literal') {
      return addMeta(nodeTypes.function.buildNode('and', [query]), text(), location());
    }
    return query;
  }
  / whitespace:[\ \t\r\n]* {
    return addMeta(nodeTypes.function.buildNode('is', '*', '*', false), text(), location());
  }

OrQuery
  = left:AndQuery space 'or'i space right:OrQuery {
    return addMeta(nodeTypes.function.buildNode('or', [left, right]), text(), location());
  }
  / AndQuery

AndQuery
  = left:NegatedClause space 'and'i space right:AndQuery {
    return addMeta(nodeTypes.function.buildNode('and', [left, right]), text(), location());
  }
  / left:NegatedClause space !'or'i right:AndQuery {
    return addMeta(nodeTypes.function.buildNode('and', [left, right]), text(), location());
  }
  / NegatedClause

NegatedClause
  = [!] clause:Clause {
    return addMeta(nodeTypes.function.buildNode('not', clause), text(), location());
  }
  / Clause

Clause
  = '(' subQuery:start ')' {
    return subQuery;
  }
  / Term

Term
  = field:literal_arg_type ':' value:literal_arg_type {
    return addMeta(nodeTypes.function.buildNodeWithArgumentNodes('is', [field, value, nodeTypes.literal.buildNode(true)]), text(), location());
  }
  / field:literal_arg_type ':[' space? gt:literal_arg_type space 'to'i space lt:literal_arg_type space? ']' {
    return addMeta(nodeTypes.function.buildNodeWithArgumentNodes('range', [field, gt, lt]), text(), location());
  }
  / function
  / !Keywords literal:literal_arg_type { return literal; }

function_name
 = first:[a-zA-Z]+ rest:[.a-zA-Z0-9_-]* { return first.join('') + rest.join('') }

function "function"
 = name:function_name space? '(' space? arg_list:arg_list? space? ')' {
  return addMeta(nodeTypes.function.buildNodeWithArgumentNodes(name, arg_list || []), text(), location());
 }

arg_list
 = first:argument rest:(space? ',' space? arg:argument {return arg})* space? ','? {
  return [first].concat(rest);
 }

argument
 = name:function_name space? '=' space? value:arg_type {
  return addMeta(nodeTypes.namedArg.buildNode(name, value), text(), location());
 }
 / element:arg_type {return element}

arg_type
 = OrQuery
 / literal_arg_type

literal_arg_type
 = literal:literal {
  var result = addMeta(nodeTypes.literal.buildNode(literal), text(), location());
  return result;
 }

Keywords
  = 'and'i / 'or'i

 /* ----- Core types ----- */

literal "literal"
  =  '"' chars:dq_char* '"' { return chars.join(''); } // double quoted string
  / "'" chars:sq_char* "'" { return chars.join(''); } // single quoted string
  / 'true'  { return true; } // unquoted literals from here down
  / 'false' { return false; }
  / 'null'  { return null; }
  / string:[^\[\]()"',:=\ \t]+ { // this also matches numbers via Number()
   var result = string.join('');
   // Sort of hacky, but PEG doesn't have backtracking so
   // a number rule is hard to read, and performs worse
   if (isNaN(Number(result))) return result;
   return Number(result)
  }

space
 = [\ \t\r\n]+

dq_char
  = "\\" sequence:('"' / "\\") { return sequence; }
  / [^"] // everything except "

sq_char
  = "\\" sequence:("'" / "\\") { return sequence; }
  / [^'] // everything except '

integer
 = digits:[0-9]+ {return parseInt(digits.join(''))}

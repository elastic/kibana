load("@npm//@bazel/typescript:index.bzl", "ts_config")
load("@build_bazel_rules_nodejs//:index.bzl", "js_library")
load("@npm//webpack-cli:index.bzl", webpack = "webpack_cli")
load("//src/dev/bazel:index.bzl", "pkg_npm", "ts_project")

PKG_DIRNAME = "kbn-ui-shared-deps-src"
PKG_REQUIRE_NAME = "@kbn/ui-shared-deps-src"

SOURCE_FILES = glob(
  [
    "**/*.ts",
    "**/*.js",
  ],
  exclude = [
    "**/*.config.js",
    "**/*.mock.*",
    "**/*.test.*",
    "**/*.stories.*",
    "**/__snapshots__/**",
    "**/integration_tests/**",
    "**/mocks/**",
    "**/scripts/**",
    "**/storybook/**",
    "**/test_fixtures/**",
    "**/test_helpers/**",
  ],
)

SRCS = SOURCE_FILES

filegroup(
  name = "srcs",
  srcs = SRCS,
)

NPM_MODULE_EXTRA_FILES = [
  "package.json",
]

# deps needed when importing this pacakge
RUNTIME_DEPS = [
]

# deps needed when building the dll
WEBPACK_DEPS = [
  "@npm//webpack",
  "@npm//mini-css-extract-plugin",
  "@npm//moment",
  "@npm//babel-loader",
  "@npm//css-loader",
  "@npm//url-loader",
  "//packages/kbn-ui-shared-deps-npm",
  "//packages/kbn-babel-register",
  "//packages/kbn-babel-preset",
  # packages included in the shared deps src bundle
  "//packages/kbn-flot-charts",
  "//packages/kbn-ui-theme",
  "//packages/kbn-i18n",
  "//packages/kbn-i18n-react",
  "//packages/kbn-monaco",
  "//packages/kbn-datemath",
  "//packages/kbn-analytics",
  "//packages/kbn-es-query",
  "//packages/kbn-std",
  "//packages/kbn-safer-lodash-set",
  "//packages/kbn-peggy",
  "//packages/kbn-peggy-loader",
  "//packages/kbn-rison",
]

# deps needed when type checking this package
TYPES_DEPS = [
  "@npm//webpack",
  "@npm//mini-css-extract-plugin",
  "@npm//moment",
  "@npm//css-loader",
  "@npm//url-loader",
  "//packages/kbn-ui-shared-deps-npm:npm_module_types",
  "//packages/kbn-babel-register:npm_module_types",
  # packages included in dll
  "//packages/kbn-ui-theme:npm_module_types",
  "//packages/kbn-i18n:npm_module_types",
  "//packages/kbn-i18n-react:npm_module_types",
  "//packages/kbn-monaco:npm_module_types",
  "//packages/kbn-datemath:npm_module_types",
  "//packages/kbn-analytics:npm_module_types",
  "//packages/kbn-es-query:npm_module_types",
  "//packages/kbn-std:npm_module_types",
  "//packages/kbn-safer-lodash-set:npm_module_types",
]

ts_config(
  name = "tsconfig",
  src = "tsconfig.json",
  deps = [
    "//:tsconfig.base.json",
    "//:tsconfig.bazel.json",
  ],
)

ts_project(
  name = "tsc_types",
  args = ['--pretty'],
  srcs = SRCS,
  deps = TYPES_DEPS,
  allow_js = True,
  declaration = True,
  emit_declaration_only = True,
  out_dir = "target_types",
  tsconfig = ":tsconfig",
)

webpack(
  name = "shared_built_assets",
  data = RUNTIME_DEPS + WEBPACK_DEPS + [
    "//:.browserslistrc",
    ":srcs",
    ":webpack.config.js",
  ],
  output_dir = True,
  args = [
    "--config",
    "$(location webpack.config.js)",
    "--output-path",
    "$(@D)",
    "--stats=errors-only"
  ],
  env = select({
    "//:dist": {
      "NODE_ENV": "production",
    },
    "//conditions:default": {
      "NODE_ENV": "development",
    },
  }),
  visibility = ["//visibility:public"],
)

js_library(
  name = PKG_DIRNAME,
  srcs = NPM_MODULE_EXTRA_FILES + SRCS,
  deps = RUNTIME_DEPS + [],
  package_name = PKG_REQUIRE_NAME,
  visibility = ["//visibility:public"],
)

js_library(
  name = "npm_module_types",
  srcs = NPM_MODULE_EXTRA_FILES,
  deps = TYPES_DEPS + [":tsc_types"],
  package_name = PKG_REQUIRE_NAME,
  visibility = ["//visibility:public"],
)

pkg_npm(
  name = "build_types",
  deps = [":npm_module_types"],
  visibility = ["//visibility:public"],
)
